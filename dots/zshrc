# Appends every command to the history file once it is executed
setopt INC_APPEND_HISTORY
# Reloads the history whenever you use it
setopt EXTENDED_HISTORY

HISTSIZE=100000
SAVEHIST=100000
HISTFILE=~/.zsh_history

export TERM=xterm-256color

if [ -f ${HOME}/.local/venv/bin/activate ]; then
    source ${HOME}/.local/venv/bin/activate
elif [ -f ${HOME}/.venv/bin/activate ]; then
    source ${HOME}/.venv/bin/activate
fi

alias vi-machine-specific="vim ~/.zsh/machine-specific.sh"

# Adapted from code found at <https://gist.github.com/1712320>.
autoload -U colors && colors
setopt prompt_subst

# Symbols / colors
GIT_PROMPT_AHEAD="%{$fg[red]%}↑NUM%{$reset_color%}"
GIT_PROMPT_BEHIND="%{$fg[cyan]%}↓NUM%{$reset_color%}"
GIT_PROMPT_MERGING="%{$fg[magenta]%}⚡︎%{$reset_color%}"
GIT_PROMPT_UNTRACKED="%{$fg[red]%}?%{$reset_color%}"
GIT_PROMPT_MODIFIED="%{$fg[yellow]%}±%{$reset_color%}"
GIT_PROMPT_STAGED="%{$fg[green]%}●%{$reset_color%}"

git_prompt_string() {
  # One git call gets everything
  local raw
  raw=$(git status --porcelain=v2 --branch 2>/dev/null) || return

  local branch="" ahead=0 behind=0 sha=""
  local staged=0 modified=0 untracked=0 merging=0
  local line

  for line in ${(f)raw}; do
    case $line in
      '# branch.head '*)  branch=${line#\# branch.head } ;;
      '# branch.ab '*)
        # format: # branch.ab +N -M
        ahead=${${line#*+}%% *}
        behind=${line##*-}
        ;;
      '# branch.oid '*)   sha=${${line#\# branch.oid }[1,7]} ;;
      '? '*)              untracked=1 ;;
      1\ ?[MTADRC]\ *|2\ ?[MTADRC]\ *)  modified=1 ;;   # worktree changes
      1\ [MTADRC]?\ *|2\ [MTADRC]?\ *)  staged=1 ;;     # index changes
    esac
  done

  [[ $branch == "(detached)" ]] && branch=${sha}

  # Check for merge/rebase (still basically free — pure builtins)
  local gitdir
  gitdir=$(git rev-parse --git-dir 2>/dev/null)
  [[ -f "$gitdir/MERGE_HEAD" ]] && merging=1

  # Build state string
  local state=""
  (( ahead > 0 ))   && state+=${GIT_PROMPT_AHEAD//NUM/$ahead}
  (( behind > 0 ))  && state+=${GIT_PROMPT_BEHIND//NUM/$behind}
  (( merging ))      && state+=$GIT_PROMPT_MERGING
  (( untracked ))    && state+=$GIT_PROMPT_UNTRACKED
  (( modified ))     && state+=$GIT_PROMPT_MODIFIED
  (( staged ))       && state+=$GIT_PROMPT_STAGED
  [[ -n $state ]]    && state="$state "

  echo " (${state}${branch} ${sha})"
}

_compute_hostname_color() {
  case $HOST in
    teller)   _HOST_COLOR='%F{cyan}' ;;
    tanoshii) _HOST_COLOR='%F{green}' ;;
    slug)     _HOST_COLOR='%F{magenta}' ;;
    *)
      local hash=$(( $(echo -n "$HOST" | cksum | cut -d' ' -f1) % 256 ))
      (( hash < 4 )) && hash=$(( hash + 4 ))
      _HOST_COLOR="%F{$hash}"
      ;;
  esac
}
_compute_hostname_color

PROMPT='%(?..%F{red}[%?] )%f%* %F{210}%n@${_HOST_COLOR}%M%f %B%F{220}%2/%f%b$(git_prompt_string) %# '

bindkey -e
export NOTIFY_COMMAND_COMPLETE_TIMEOUT=15

# Quick cd tool
. ~/.sh/z.sh

export MARKPATH=$HOME/.marks
function jump {
    cd -P "$MARKPATH/$1" 2>/dev/null || echo "No such mark: $1"
}
function mark {
    mkdir -p "$MARKPATH"; ln -s "$(pwd)" "$MARKPATH/$1"
}
function unmark {
    rm -i "$MARKPATH/$1"
}
function marks {
    ls -l "$MARKPATH" | sed 's/  / /g' | cut -d' ' -f9- | sed 's/ -/\t-/g' && echo
}

function _completemarks {
  reply=($(ls $MARKPATH))
}

compctl -K _completemarks jump
compctl -K _completemarks unmark

source ~/.sh/aliases.sh
source ~/.sh/all.sh

unsetopt correct_all
# setopt no_share_history

autoload -U compinit && compinit

zstyle ':completion:*' menu select

if [ -f ~/.sh/fzf.zsh ]; then 
  source ~/.sh/fzf.zsh
  source ~/.sh/fzf-tab.zsh
fi

export FZF_DEFAULT_COMMAND="rg --files --follow --no-ignore-vcs --hidden -g '!.git/' -g '!.pytest_cache/' -g '!**/.*.egg-info/' -g '!*.pyc' -g '!node_modules/' -g '!__pycache__/' -g '!.mypy_cache/' -g '!.cache/' -g '!venv/'"

[ ! -z "$DESK_ENV" ] && source "$DESK_ENV"

export PYTHONSTARTUP="${HOME}/.config/python/startup.py"

# Get pass to default to the yubikey
export PASSWORD_STORE_GPG_OPTS='--default-key 0xC3ED8EEC97AA9D43 '

export PATH=${HOME}/.local/bin:${PATH}

# Have zsh report time
# REPORTTIME=3

if `which ruby >/dev/null`; then
  export GEM_HOME="$HOME/.local/gems"
  export PATH="$HOME/.local/gems/bin:$PATH"
fi

if [ -d "${HOME}/.cargo/bin" ]; then
  export PATH="${HOME}/.cargo/bin:${PATH}"
fi

if [ -d "/usr/local/go/bin" ]; then
  export PATH="${PATH}:/usr/local/go/bin"
fi

export GDK_BACKEND=wayland
export QT_QPA_PLATFORM=wayland

export SWAYSOCK=/run/user/$(id -u)/sway-ipc.$(id -u)..sock

export BAT_THEME="1337"
# export BAT_THEME="zenburn"
export MANPAGER=less

export LD_LIBRARY_PATH=${LD_LIBRARY_PATH}:/usr/local/lib

export EDITOR=vim

if [ "$(tty)" = "/dev/tty1" ] && which sway >/dev/null; then
    export SDL_VIDEODRIVER=wayland
    export _JAVA_AWT_WM_NONREPARENTING=1
    export QT_QPA_PLATFORM=wayland
    export XDG_CURRENT_DESKTOP=sway
    export XDG_SESSION_DESKTOP=sway
    sway
fi

if [ -d /opt/android-sdk ]; then
    export ANDROID_HOME='/opt/android-sdk'
    export PATH=${PATH}:"$ANDROID_HOME/tools/bin/"
    export PATH=${PATH}:"$ANDROID_HOME/platform-tools/"
    export PATH=${PATH}:"$ANDROID_HOME/cmdline-tools/latest/bin"
    export PATH=${PATH}:"$ANDROID_HOME/emulator"
fi

# Gets `thousands_sep` to work.
export PYTHONPATH=${HOME}/.ipython/extensions:${PYTHONPATH}

# Start tmux unless in an ssh connection.
#
# 2024-01-03 Wed 10:32
# This was causing problems with running vanilla zsh commands (e.g. `getpass`) so 
# I'm instead doing this as a specific case in the sawy config.
#
# if [ -z "$TMUX" ] && [ -z "$SSH_CONNECTION" ] && [ -z "$NO_TMUX" ] && [ "$(tty)" != "/dev/tty1" ]; then 
    # tmux $@
# fi

# Spawn an ssh-agent and link it to `~/.ssh/ssh_auth_sock`. This is so
# we can do things like git pushes from cronjobs and services while unattended.
#
# I don't think it represents any compromise in security since these same
# sockets are living in /tmp anyway, so anyone who is the james user can 
# make use of any of them...
#
# See: https://unix.stackexchange.com/a/217223
#
# if [ ! -S ~/.ssh/ssh_auth_sock ]; then
#   eval `ssh-agent -t $(( 60 * 60 * 24 ))`
#   ln -sf "$SSH_AUTH_SOCK" ~/.ssh/ssh_auth_sock
# fi
# export SSH_AUTH_SOCK=~/.ssh/ssh_auth_sock
# ssh-add -l > /dev/null || ssh-add

export GPG_TTY="$(tty)"
if [[ "$OSTYPE" == "darwin"* ]]; then
    export SSH_AUTH_SOCK=$(gpgconf --list-dirs agent-ssh-socket)
else
    export SSH_AUTH_SOCK=${SSH_AUTH_SOCK:-$(gpgconf --list-dirs agent-ssh-socket)}
fi
gpgconf --launch gpg-agent
gpg-connect-agent updatestartuptty /bye > /dev/null

if [ -f ~/.zsh/machine-specific.sh ]; then
    . ~/.zsh/machine-specific.sh
fi
